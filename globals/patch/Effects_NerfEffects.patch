% Nerf some effects when there's too many enemies in a battle, needed for the all bosses fight
% to stop some crashes.

%=================================================
% 001E0000 / ~FX:ShowDamageAmount
%
% Don't show the little stars if 9 or more enemies are in battle
@Function 34F50C % E003C04C
{
    LIO     V0, $Hook_E003C04C
    JR      V0
    NOP
}

#new:Function $Hook_E003C04C
{
    PUSH    RA, A0, A1, A2, A3, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .ShowStars
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .ShowStars
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .ShowStars
    NOP
    % Skip FX
    SLTI	V0, S2, 0002 % If damage value is >=2 don't show any stars
    BNE 	V0, R0, .E003C05C
    COPY    S0, S2
    BEQ     R0, R0, .E003C05C
    ADDIU	S2, R0, 0001
    .ShowStars
    SLTI	V0, S2, 0015
    BNE 	V0, R0, .E003C05C
    COPY    S0, S2
    ADDIU	S2, R0, 0014
    .E003C05C
    ADDIU	S2, S2, 0001
    % go back
    POP     RA, A0, A1, A2, A3, V1
    LIO     V0, E003C060
    JR      V0
    NOP
}

@Function 34FF98 % E003CAD8
{
    LIO     V1, $Hook_E003CAD8
    JR      V1
    NOP
}

#new:Function $Hook_E003CAD8
{
    % restore vanilla code
    SW	    S5, 0008 (A1)
    SW	    V0, 000C (A1)
    LW	    T2, 00A8 (SP)
    PUSH    RA, A0, A1, A2, A3
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .DontForce2Digits
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .DontForce2Digits
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .DontForce2Digits
    NOP
    % Force 2 digits to always appear in the damage counter needed for
    % the above hook to work on all amounts and not just when the value is >=10
    BEQ     R0, R0, .End
    ORI 	V0, R0, 0000 % False
    .DontForce2Digits
    SLTI	V0, T2, 000A
    .End
    POP     RA, A0, A1, A2, A3
    LIO     V1, E003CAE8
    JR      V1
    NOP
}

%=================================================
% 00430000 / ~FX:Firework:White
%
% Entirely skip this effect when 9 or more enemies are in battle
%
% From ~Func:playFX_43
@Hook 4C5D4 % 800711D4
{
    LW	    A0, 0010 (SP) % load subFXID
    PUSH    RA, A0, A1, A2, A3, V0, V1
    BNE     A0, R0, .End
    NOP
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V0, V1
    % POP - 2
    LW	    A0, 0010 (SP)
    LW	    A1, 0014 (SP)
    LW	    A2, 0018 (SP)
    LW	    A3, 001C (SP)
    LWC1	F12, 0020 (SP)
    LWC1	F14, 0024 (SP)
    LWC1	F16, 0028 (SP)
    LW	    RA, 002C (SP)
    ADDIU	SP, SP, 30
    JR      RA
    NOP
    .End
    % restore vanilla code
    %
    % play FX
    POP     RA, A0, A1, A2, A3, V0, V1
    JAL     ~Func:load_effect
    ADDIU	A0, R0, 0043
    LW	    A0, 0010 (SP)
    LW	    A1, 0014 (SP)
    LW	    A2, 0018 (SP)
    LW	    A3, 001C (SP)
    LWC1	F12, 0020 (SP)
    LWC1	F14, 0024 (SP)
    LWC1	F16, 0028 (SP)
    LW	    RA, 002C (SP)
    ADDIU	SP, SP, 30
    LUI	    AT, 8008
    LW	    AT, F858 (AT)
    JR	    AT
    NOP
}

#new:Function $Hook_E0086000
{
    PUSH    RA, A0, A1, A2, A3, V1
    % A0 = subFxID
    BNE     A0, R0, .End
    NOP
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V1
    JR      RA
    NOP
    .End
    POP     RA, A0, A1, A2, A3, V1
    % restore vanilla code
    ADDIU	SP, SP, -68
    SW	    S0, 0028 (SP)
    LIO     V0, E0086010
    JR      V0
    COPY    S0, A0
}

%============================================================
% Disable the "Nice", "Good", "Lucky" popup battle dispacher
%
% ~Func:show_action_rating
@Hook 1950D0 % 802667F0
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V0, V1
    JR      RA
    NOP
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    ADDIU	SP, SP, -20
    J       802667F8
    MTC1	A2, F0
}

%============================================================
% 1 - Don't show one of the multiple water FXs when an attack has the ~DamageType:Water flag
%
% From ~Func:calc_partner_damage_enemy
@Hook 1AD16C % 8027E88C
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V0, V1
    J       8027E8C0
    NOP
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LWC1	F4, 0014 (S2)
    J       8027E894
    LUI	    AT, 40A0
}

% 2 - Don't show one of the multiple water FXs when an attack has the ~DamageType:Water flag
%
% From ~Func:create_element_hit_effect
@Hook 195048 % 80266768
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V0, V1
    J       802667E4
    NOP
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    MFC1	A1, F0
    J       80266770
    MFC1	A2, F2
}

%=================================================
% Don't Render Sprites when a menu pop-up is open
%=================================================

#export:Data $PopupMenu_Byte
{ 00000000 }

%============
% Set Byte
%============

% Set $PopupMenu_Byte as true
%
% ~Func:popup_menu_draw_title_contents
@Hook 08DAF8 % 800F4648
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    ORI     A1, R0, 1
    SAW     A1, $PopupMenu_Byte
    % restore vanilla code
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LUI	    V0, 8011
    J       800F4650
    LW	    V0, D69C (V0)
}

% Set $PopupMenu_Byte as true
%
% func_802A2C84 / ~Func:battle_popup_menu_draw_contents(?)
@Hook 417A14 % 802A2C84
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    ORI     A1, R0, 1
    SAW     A1, $PopupMenu_Byte
    % restore vanilla code
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LUI	    V0, 802B
    J       802A2C8C
    LBU	    V0, D108 (V0)
}

% Set $PopupMenu_Byte as true
%
% func_802A4A54 / ~Func:battle_strategies_menu_draw_contents(?)
@Hook 4197E4 % 802A4A54
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if player is inside the "All bosses" battle
    % and 9 or more enemies are in the battle
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1 % battle
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    ORI     A1, R0, 1
    SAW     A1, $PopupMenu_Byte
    % restore vanilla code
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LUI	    V0, 802B
    J       802A4A5C
    LBU	    V0, D604 (V0)
}

%============
% Clear Byte
%============

% Clear $PopupMenu_Byte
%
% ~Func:hide_popup_menu
@Hook 08A860 % 800F13B0
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    LUI	    V0, 8011
    J       800F13B8
    LB	    V0, D68E (V0)
}

% Clear $PopupMenu_Byte
%
% ~Func:destroy_popup_menu
@Hook 08A9E8 % 800F1538
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    LUI	    A0, 8011
    J       800F1540
    LW	    A0, D65C (A0)
}


% Close popup menu
%
% func_802A2C84 / ~Func:battle_popup_menu_draw_contents(?)
@Hook 41863C % 802A38AC
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    LUI	    V0, 802B
    J       802A38B4
    LW	    V0, D4A0 (V0)
}

% Hide popup menu
%
% func_802A2C84 / ~Func:battle_popup_menu_draw_contents(?)
@Hook 418678 % 802A38E8
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    LUI     V0, 802B
    J       802A38F0
    LW	    V0, D4A0 (V0)
}

% Close popup menu
%
% func_802A4A54 / ~Func:battle_strategies_menu_draw_contents(?)
@Hook 419DE4 % 802A5054
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    J	    802A5274
    ADDIU	V0, R0, 00FF
}

% Hide popup menu
%
% func_802A4A54 / ~Func:battle_strategies_menu_draw_contents(?)
@Hook 419E18 % 802A5088
{
    SAW     R0, $PopupMenu_Byte
    % restore vanilla code
    J	    802A50D0
    ADDIU	V1, R0, 000B
}

%%%%

% Don't render any actor when a menu is opened
%
% ~Func:spr_draw_component
@Hook 0FFFCC % 802DCEDC
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if $PopupMenu_Byte is true
    LAW     A0, $PopupMenu_Byte
    BEQ     A0, R0, .End
    NOP
    % Skip FX
    POP     RA, A0, A1, A2, A3, V0, V1
    JR      RA % skip this function
    NOP
    % restore vanilla code
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    ADDIU	SP, SP, -78
    J       802DCEE4
    SW	    S3, 0054 (SP)
}

#new:Function $GetAmountOfEnemiesInBattle
{
    PUSH    RA, A0, A1, A2, A3, V1, S0
    COPY    S0, A0
    CLEAR   V0
    LIO     A0, 800DC150 % ActorTable(0x0C)=Enemies Data - Start
    ORI     A3, R0, 24` % because last enemy is Enemy23
    SLL     A3, A3, 2
    ADDU    A3, A0, A3
    .Loop
    LW      A1, 0 (A0)
    BNEL    A1, R0, .Add1ToCounter
    ADDI    V0, V0, 1
    .Add1ToCounter
    ADDI    A0, A0, 4
    BNE     A0, A3, .Loop
    NOP
    POP     RA, A0, A1, A2, A3, V1, S0
    JR      RA
    NOP
}

/%  Scraped

Lower the amount of possible FXs to be render per frame, this is neccesary for the Mega Quacke Badge
%
% ~Func:update_effects
@Hook 35254 % 80059E54
{
    PUSH    RA, A0, A1, A2, A3, V1
    LABU    A0, 80074094 % current mode
    ORI     A1, R0, 1
    BNE     A0, A1, .End
    NOP
    % Check if current battle is 2F00
    LAH     A0, 800DC4EA % battleID
    ORI     A1, R0, 2F00
    BNE     A0, A1, .End
    NOP
    % check amount of enemies in battle
    JAL     $GetAmountOfEnemiesInBattle
    NOP
    SLTIU   A1, V0, 9
    BNE     A1, R0, .End
    NOP
    % There's too many enemies, reduce the FX cap
    BEQ     R0, R0, .Pop
    SLTI	V0, S2, 0035
    .End
    SLTI	V0, S2, 0060
    .Pop
    POP     RA, A0, A1, A2, A3, V1
    BNE 	V0, R0, .80059DCC
    ADDIU	S1, S1, 0004
    .80059E60    
    J       80059E60
    NOP
    .80059DCC
    J       80059DCC
    NOP
}%/
